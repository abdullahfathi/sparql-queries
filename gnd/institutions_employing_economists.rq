# Names of institutions which employ economists
#
PREFIX :        <https://d-nb.info/gnd/>
PREFIX gndo:    <https://d-nb.info/standards/elementset/gnd#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
#
select distinct ?corp ?corpLabel ?area ?areaLabel ?location ?locationLabel
where {
  {
    ?subjectHeading gndo:preferredNameForTheSubjectHeading "Wirtschaftswissenschaftler"
  } union {
    ?subjectHeading gndo:preferredNameForTheSubjectHeading "Wirtschaftswissenschaftlerin"
  } .
  ?person gndo:professionOrOccupation ?subjectHeading ;
    gndo:affiliation ?corp .
  ?corp gndo:preferredNameForTheCorporateBody ?corpLabel .
  #
  # area code sometimes missing
  optional {
    ?corp gndo:geographicAreaCode ?area .
    # area code seems to be missing in the GND dataset
    ##?area skos:prefLabel ?areaLabel .
    bind(strafter(str(?area), '#') as ?areaLabel)
  }
  #
  # places sometimes missing
  optional {
    ?corp gndo:placeOfBusiness ?location .
    ?location gndo:preferredNameForThePlaceOrGeographicName ?locationLabel .
  }
}
order by ?corpLabel
