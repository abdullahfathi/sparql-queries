# Get all attributes for URIs from econ_corp and get preferred names
# for this attributes (if these are URIs)
#
PREFIX gndo:    <http://d-nb.info/standards/elementset/gnd#>
PREFIX :        <http://d-nb.info/gnd/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX zbwext:  <http://zbw.eu/namespaces/zbw-extensions/>
#
CONSTRUCT {
  ?corp ?p ?o .
  ?o ?preferredNameProperty ?name .
}
WHERE {
  # extract all subjects from econ_corp
  SERVICE <http://zbw.eu/beta/sparql/econ_corp/query> {
    ?corp ?tmpProp [] .
  }
  ?corp a gndo:CorporateBody ;
        ?p ?o .

  OPTIONAL {
    FILTER(isURI(?o))
    ?o ?preferredNameProperty ?name .
    FILTER(?preferredNameProperty IN (
        rdf:type,
        gndo:preferredName,
        gndo:preferredNameForTheCorporateBody,
        gndo:preferredNameForThePerson,
        gndo:preferredNameForThePlaceOrGeographicName,
        gndo:preferredNameForTheSubjectHeading,
        gndo:preferredNameForTheWork
    ))
  }
}
