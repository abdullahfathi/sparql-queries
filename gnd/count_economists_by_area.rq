# Geographical distribution of economists (in GND)
#
PREFIX :        <https://d-nb.info/gnd/>
PREFIX gndo:    <https://d-nb.info/standards/elementset/gnd#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix area: <https://d-nb.info/standards/vocab/gnd/geographic-area-code#>
#
select (str(count(?person)) as ?count) ?area
where {
  {
    ?subjectHeading gndo:preferredNameForTheSubjectHeading "Wirtschaftswissenschaftler"
  } union {
    ?subjectHeading gndo:preferredNameForTheSubjectHeading "Wirtschaftswissenschaftlerin"
  } .
  ?person gndo:professionOrOccupation ?subjectHeading .
  ?person a gndo:DifferentiatedPerson .
  ?person gndo:geographicAreaCode ?area
}
group by ?area
order by desc(count(?person))

