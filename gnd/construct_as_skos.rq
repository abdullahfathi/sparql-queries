# Extract core SWD as skos thesaurus
#
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gndsc: <http://d-nb.info/standards/vocab/gnd/gnd-sc#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gnd: <http://d-nb.info/gnd/>
PREFIX stw: <http://zbw.eu/stw/descriptor/>
PREFIX ddc: <http://dewey.info/class/>
#
construct {
  ?concept a skos:Concept ;
    dc:identifier ?id ;

    # labels
    skos:prefLabel ?prefLabel ;
    skos:altLabel ?altLabel ;

    # existing relations
    skos:narrower ?narrowerConcept ;
    skos:broader ?broaderConcept ;
    skos:related ?relatedConcept ;

    # matches to other vocabs
    skos:exactMatch ?emConcept ;
    skos:closeMatch ?cmConcept ;
    skos:broadMatch ?bmConcept ;
    skos:narrowMatch ?nmConcept ;
    skos:relatedMatch ?rmConcept ;

    # subject categories
    skos:member ?subjectCategory ;
  .

  # missing reverse relations
  ?broaderConcept skos:narrower ?concept .
  ?relatedConcept skos:related ?concept .

  # original relations
  ?concept ?originalRelation ?broaderConcept .
  ?concept ?originalRelation ?narrowerConcept .
  ?concept ?originalRelation ?relatedConcept .
}
where {
  values ( ?concept )
  {
    ( <http://d-nb.info/gnd/4056366-2> )
    ##( undef )
  }
  ?concept a gndo:SubjectHeadingSensoStricto ;
           gndo:preferredNameForTheSubjectHeading ?prefLabel ;
           gndo:gndIdentifier ?id .
  optional {
    ?concept gndo:variantNameForTheSubjectHeading  ?altLabel
  }
  # dump down different kinds of broader/narrower links
  optional {
    ?concept gndo:broaderTerm|gndo:broaderTermGeneral|gndo:broaderTermGeneric|gndo:broaderTermInstantial|gndo:broaderTermPartitive ?broaderConcept .
    ?concept ?originalRelation ?broaderConcept .
  }
  optional {
    ?concept gndo:narrowerTerm|gndo:narrowerTermGeneral|gndo:narrowerTermGeneric|gndo:narrowerTermInstantial|gndo:narrowerTermPartitive ?narrowerConcept .
    ?concept ?originalRelation ?narrowerConcept .
  }
  optional {
    ?concept gndo:relatedTerm ?relatedConcept .
    ?concept ?originalRelation ?relatedConcept .
  }

  # matches to other vocabs
  optional {
    ?concept skos:exactMatch ?emConcept
  }
  optional {
    ?concept skos:closeMatch ?cmConcept
  }
  optional {
    ?concept skos:relatedMatch ?rmConcept
  }
  optional {
    ?concept skos:broadMatch ?bmConcept
  }
  optional {
    ?concept skos:narrowMatch ?nmConcept
  }

  # ddc exact matches
##  optional {
##    ?concept gndo:relatedDdcWithDegreeOfDeterminacy4 ?emConcept
##  }

  # dump down ddc close matches (skip very loose matching concepts)
##  optional {
##    ?concept gndo:relatedDdcWithDegreeOfDeterminacy3|gndo:relatedDdcWithDegreeOfDeterminacy2 ?emConcept
##  }

  optional {
    ?concept gndo:gndSubjectCategory  ?subjectCategory
  }
}

