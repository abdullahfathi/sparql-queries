# Corporate bodies which employ people with a profession
# relevant to economics or social sciences
#
PREFIX gndo:    <http://d-nb.info/standards/elementset/gnd#>
PREFIX :        <http://d-nb.info/gnd/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX zbwext:  <http://zbw.eu/namespaces/zbw-extensions/>

CONSTRUCT {
  ?corp zbwext:affiliatedCount ?affiliatedCount .
}
WHERE {
  SELECT ?corp (COUNT(DISTINCT ?pers) AS ?affiliatedCount)
  WHERE {
    # to be replaced by a list of profession names from professions.lst
    VALUES ?professionPrefLabel {
      "Wirtschaftswissenschaftler"
    }
    ?profession gndo:preferredNameForTheSubjectHeading ?professionPrefLabel .
    ?pers gndo:professionOrOccupation ?profession .
    ?pers gndo:affiliation ?corp .
  }
  GROUP BY ?corp
}
