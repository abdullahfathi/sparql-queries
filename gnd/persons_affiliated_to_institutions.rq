# Names of persons affiliated to selected institutions
#
PREFIX :        <https://d-nb.info/gnd/>
PREFIX gndo:    <https://d-nb.info/standards/elementset/gnd#>
select distinct ?corp (str(?corpName) as ?corpLabel) ?person (str(?personName) as ?personLabel)
where {
  values ( ?corp )
  {
    ( <https://d-nb.info/gnd/1002465-7> )  # Bundesbank
    ( <https://d-nb.info/gnd/37419-2> )    # DIW
    ( <https://d-nb.info/gnd/16015541-1> ) # GESIS
    ( <https://d-nb.info/gnd/16345994-0> ) # IFO
    ( <https://d-nb.info/gnd/39048-3> )    # RWI
    ( <https://d-nb.info/gnd/90968-3> )    # WZB
    ( <https://d-nb.info/gnd/10158795-8> ) # ZBW
    ( <https://d-nb.info/gnd/2120785-9> )  # ZEW
    
  }
  ?corp gndo:preferredNameForTheCorporateBody ?corpName .
  ?person gndo:affiliation ?corp ;
    gndo:preferredNameForThePerson ?personName
}
order by ?corpName ?personName

