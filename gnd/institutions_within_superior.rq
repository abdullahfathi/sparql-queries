# Economics institutions within superior institutions
#
PREFIX  gndo:   <http://d-nb.info/standards/elementset/gnd#>
#
select *
where {
  # restrict to institutions relevant to economics
  service <http://zbw.eu/beta/sparql/econ_corp/query> {
    select ?gnd
    where {
      ?gnd gndo:preferredName [] .
    }
  }
  ?gnd gndo:preferredNameForTheCorporateBody ?gndLabel .
  ?gnd gndo:hierarchicalSuperiorOfTheCorporateBody ?supGnd .
  ?supGnd gndo:preferredNameForTheCorporateBody ?supGndLabel .
  #
  # exclude institutions with have succiding institutions
  # (but for now keep superior institutions which have successors)
  filter(not exists {
      ?gnd gndo:succeedingCorporateBody [] .
    }
  )
}
limit 100
