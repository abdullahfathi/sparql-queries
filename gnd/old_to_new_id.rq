# Map old PND/GKD/SWD/GND IDs to valid GND IDs
# (other ISILs, like 101c for Deutsches Musikarchiv, are mapped to type 'xxx')
#
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
#
select distinct ?type ?oldId ?id
where {
  ?s gndo:gndIdentifier ?id .
  ?s gndo:oldAuthorityNumber ?old .
  bind(strafter(?old, ')') as ?oldId)
  bind(if(contains($old, '558)'),
      'gnd',
      if(contains(?old, '588a)'),
        'pnd',
        if(contains(?old, '588b)'),
          'gkd',
          if(contains(?old, '588c)'),
            'swd',
            'xxx')))
    ) as ?type)
  filter (?id != ?oldId)
}
limit 1000
