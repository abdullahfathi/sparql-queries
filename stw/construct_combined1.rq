# Descriptors, with related and narrower descriptors, and their synonyms
#
# Search via text query (as used for the /combined1 econ-ws service)
#
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX zbwext:  <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX text:    <http://jena.apache.org/text#>
#
CONSTRUCT {
  ?concept skos:prefLabel ?prefLabel .
  ?concept skos:hiddenLabel ?hiddenLabel .
  ?concept skos:narrower ?narrower .
  ?concept skos:related ?related .
}
WHERE {
  VALUES (?q ?limit) {
    ( 'telework bank' 2 ) 
  }  
  {
    {
      ?concept text:query ( ?q ?limit ) 
    }
    UNION
    {
      ?concept text:query ( ?q ?limit ) .
      ?concept skos:narrower ?narrower
    }
    UNION
    {
      ?tmpConcept text:query ( ?q ?limit ) .
      ?tmpConcept skos:narrower ?concept
    }
    UNION
    {
      ?concept text:query ( ?q ?limit ) .
      ?concept skos:related ?related
    }
    UNION
    {
      ?tmpConcept text:query ( ?q ?limit ) .
      ?tmpConcept skos:related ?concept
    }
  }
  ?concept rdf:type zbwext:Descriptor.
  {
    {
      ?concept skos:prefLabel ?prefLabel 
    }
    UNION
    {
      ?concept skos:prefLabel ?label 
    }
    UNION
    {
      ?concept skos:altLabel ?label 
    }
    UNION
    {
      ?concept skos:hiddenLabel ?label 
    }
  }
  BIND (lcase(?label) AS ?hiddenLabel)
}

