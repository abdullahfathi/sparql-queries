# All STW descriptors mapped to Wikidata, with Wikipedia pages
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
#
select distinct ?stw ?stwLabel ?wd ?wdLabel ?wpEn ?wpEnLabel ?wpDe ?wpDeLabel
where {
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    # VALUES clause with language does not work as expected
    #
    # get all wikidata items and labels linked to STW
    ?wd wdt:P3911 ?stwId .
    optional {
      ?wd rdfs:label ?label .
      filter(lang(?label) = 'en')
      bind(str(?label) as ?wdLabel)
    }
    optional {
      # get site links (only from en wikipedia sites)
      ?wpEn schema:about ?wd ;
                schema:name ?wikipediaLabelEn;
                schema:inLanguage 'en' .
      filter (contains(str(?wpEn), 'wikipedia'))
      bind(str(?wikipediaLabelEn) as ?wpEnLabel)
    }
    optional {
      # get site links (only from de wikipedia sites)
      ?wpDe schema:about ?wd ;
                schema:name ?wikipediaLabelDe ;
                schema:inLanguage 'de' .
      filter (contains(str(?wpDe), 'wikipedia'))
      bind(str(?wikipediaLabelDe) as ?wpDeLabel)
    }
  }
  bind(uri(concat('http://zbw.eu/stw/descriptor/', ?stwId)) as ?stw)
  #
  # look up STW labels
  ?stw skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = 'en')
  bind(str(?stwLabelLang) as ?stwLabel)
}
order by ?stwLabelLang

