# Get all mappings from wikidata (with labels in English language) to STW,
# supplemented with Wikipedia site links and GND properties, if exist
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
#
select distinct ?stw ?stwLabel ?wd ?wdLabel ?wikipedia ?wikipediaLabel ?gnd (?gndId as ?gndLabel)
where {
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    # VALUES clause with language does not work as expected
    #
    # get all wikidata items and labels linked to STW
    ?wd wdt:P3911 ?stwId .
    optional {
      ?wd rdfs:label ?label .
      filter(lang(?label) = 'en')
      bind(str(?label) as ?wdLabel)
    }
    optional {
      # get site links (only from en wikipedia sites)
      ?sitelink schema:about ?wd ;
                schema:name ?wikipediaLabelLang ;
                schema:inLanguage 'en' .
      filter (contains(str(?sitelink), 'wikipedia'))
      bind(?sitelink as ?wikipedia)
      bind(str(?wikipediaLabelLang) as ?wikipediaLabel)
    }
    #
    # suplement with GND
    optional {
      ?wd wdt:P227 ?gndId
    }
  }
  bind(uri(concat('http://zbw.eu/stw/descriptor/', ?stwId)) as ?stw)
  bind(uri(concat('http://d-nb.info/gnd/', ?gndId)) as ?gnd)
  #
  # look up STW labels
  ?stw skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = 'en')
  bind(str(?stwLabelLang) as ?stwLabel)
}
order by ?stwLabelLang
