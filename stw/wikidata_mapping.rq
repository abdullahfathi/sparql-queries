# Get all mappings from Wikidata to STW (with labels in German language)
#
# needs to run from an endpoint holding the vocabulary of the external-id
# in order to get the labels
#
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#
select distinct ?stw ?relation ?relationLabel ?stwLabel ?wd ?wdLabel
where {
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    # wd items with STW ID
    ?wd wdt:P3911 ?stwId .
    filter(isLiteral(?stwId))
    #
    # mapping relation
    optional {
      ?wd p:P3911/pq:P4390 ?relation .
      ?relation rdfs:label ?relationLabelLang .
      filter(lang(?relationLabelLang) = 'en')
      bind(str(?relationLabelLang) as ?relationLabel)
    }
    # get label for the wd item, if exists
    optional {
      ?wd rdfs:label ?label .
      filter(lang(?label) = 'de')
      bind(str(?label) as ?wdLabel)
    }
  }
  # look up STW descriptors and labels
  bind(uri(concat('http://zbw.eu/stw/descriptor/', ?stwId)) as ?stw)
  ?stw skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = 'de')
  bind(str(?stwLabelLang) as ?stwLabel)
}
order by ?relationLabel ?stwLabel
