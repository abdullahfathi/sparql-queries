# Get all mappings to STW from from Wikidata (with labels in German language)
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select distinct ?stw ?stwLabel ?wd ?wdLabel
##select distinct ?stwId
where {
  # get all wikidata items and labels linked with GND (exclude persons)
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    # with GND id
    ?wd wdt:P3911 ?stwId .
    filter(isLiteral(?stwId))

    # get labels for the wd item, if exists
    optional {
      ?wd rdfs:label ?label .
      filter(lang(?label) = 'de')
      bind(str(?label) as ?wdLabel)
    }



  }
  bind(uri(concat('http://zbw.eu/stw/descriptor/', ?stwId)) as ?stw)
  #
  # look up STW descriptors and labels
  ?stw skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = 'de')
  bind(str(?stwLabelLang) as ?stwLabel)
}
order by ?stwLabelLang
