# Get all mapping candidates from wikidata (with labels in German language)
# to STW, derived from existing GND (P227) properties and the STW-GND mapping,
# supplemented with Wikipedia site links
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select distinct ?stw ?stwLabel ?gnd ?gndLabel ?wd ?wdLabel ?wikipedia ?wikipediaLabel
where {
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    # get all wikidata items and labels linked with GND (exclude persons)
    ?wd wdt:P227 ?gndId .
    filter(not exists {
        ?wd wdt:P31 wd:Q5
      })
    optional {
      ?wd rdfs:label ?label .
      filter(lang(?label) = 'de')
      bind(str(?label) as ?wdLabel)
    }
    optional {
      # get site links (only from de wikipedia sites)
      ?sitelink schema:about ?wd ;
                schema:name ?wikipediaLabelLang ;
                schema:inLanguage 'de' .
      filter (contains(str(?sitelink), 'wikipedia'))
      bind(?sitelink as ?wikipedia)
      bind(str(?wikipediaLabelLang) as ?wikipediaLabel)
    }
  }
  bind(uri(concat('http://zbw.eu/stw/descriptor/', ?stwId)) as ?stw)
  bind(uri(concat('http://d-nb.info/gnd/', ?gndId)) as ?gnd)
  #
  # look up STW descriptors and labels
  ?stw skos:exactMatch ?gnd ;
       skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = 'de')
  bind(str(?stwLabelLang) as ?stwLabel)
  #
  # look up GND labels
  ?gnd skos:prefLabel ?gndLabelLang .
  filter(lang(?gndLabelLang) = 'de')
  bind(str(?gndLabelLang) as ?gndLabel)
}
order by ?stwLabelLang
