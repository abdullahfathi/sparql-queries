# GND concepts with multiple skos:exactMatch STW concepts
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX dcterms: <http://purl.org/dc/terms/>
#
select ?gnd ?gndLabel ?stw ?stwLabel
where {
  {
    select ?gnd (count (distinct ?stw) as ?count)
    where {
      #    ?stw a zbwext:Deskriptor ;
      #       skos:exactMatch ?gnd .
      #    ?gnd dcterms:isPartOf <http://zbw.eu/stw/mapping/gnd/target> .
      ?gnd skos:exactMatch ?stw ;
           dcterms:isPartOf <http://zbw.eu/stw/mapping/gnd/target> .
      ?stw a zbwext:Descriptor .
    }
    group by ?gnd
    having (?count > 1)
    order by desc(?count)
  }
  ?gnd skos:exactMatch ?stw ;
       skos:prefLabel ?gndLabel .
  ?stw a zbwext:Descriptor ;
       skos:prefLabel ?stwLabel .
  filter(lang(?stwLabel) = 'de')
}
order by ?gndLabel
