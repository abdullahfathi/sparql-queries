# Mapped concepts with multiple exact matching STW concepts
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX dcterms: <http://purl.org/dc/terms/>
#
select ?foreign ?foreignLabel ?target ?stw ?stwLabel
where {
  {
    select ?foreign (count (distinct ?stw) as ?count) ?target
    where {
      ?foreign skos:exactMatch ?stw ;
           dcterms:isPartOf ?target .
      ?stw a zbwext:Descriptor .
    }
    group by ?foreign ?target
    having (?count > 1)
    order by desc(?count)
  }
  # add labels
  ?foreign skos:exactMatch ?stw ;
       skos:prefLabel ?foreignLabel .
  ?stw a zbwext:Descriptor ;
       skos:prefLabel ?stwLabel .
  filter(lang(?stwLabel) = 'de')
}
order by ?foreignLabel
