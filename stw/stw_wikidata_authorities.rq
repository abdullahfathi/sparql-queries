# All STW descriptors mapped to Wikidata, with selected authorities
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
select distinct ?stw ?stwLabel ?stwId ?wd (str(?wdLabelLang) as ?wdLabel)
?gnd (?gndId as ?gndLabel)
?viaf (?viafId as ?viafLabel)
?lcAuth (?lcAuthId as ?lcAuthLabel)
where {
  # get all wikidata entries for humans with ras
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    ?wd wdt:P3911 ?stwId .
    #
    # optional, because English label may be missing
    optional {
      ?wd rdfs:label ?wdLabelLang .
      filter(lang(?wdLabelLang) = 'en')
    }
    #
    optional {
      ?wd wdt:P227 ?gndId
    }
    optional {
      ?wd wdt:P214 ?viafId
    }
    optional {
      ?wd wdt:P244 ?lcAuthId
    }
  }
  bind(uri(concat("http://zbw.eu/stw/descriptor/", $stwId)) as ?stw)
  bind(uri(concat("http://d-nb.info/gnd/", $gndId)) as ?gnd)
  bind(uri(concat("https://viaf.org/viaf/", ?viafId)) as ?viaf)
  bind(uri(concat("http://id.loc.gov/authorities/", ?lcAuthId)) as ?lcAuth)
  #
  ?stw skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = 'en')
}
order by asc(?stwLabel)
