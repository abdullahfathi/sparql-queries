# All (lower-cased) labels for a GND concept
# and its (via STW) exact-matching counterparts in other
# vocabularies
#
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
#
select distinct ?id (sample(?foreign) as ?concept) (str(sample(?prefLabel)) as ?conceptLabel)
    (group_concat(?synonym; separator = ' | ') as ?synonyms)
where {
  # nested select necessary for excluding duplicates _before_ concatenating
  # (for highlighting in EconBiz, labels should not be lowercased)
  select distinct ?id ?foreign ?prefLabel (str(?label) as ?synonym)
  where {
    # obtain gnd concepts via their mapping to STW
    ?stw a zbwext:Descriptor ;
         skos:exactMatch ?foreign .
    ?foreign dcterms:isPartOf <http://zbw.eu/stw/mapping/gnd/target> ;
             skos:prefLabel ?prefLabel .
    {
      # include all labels from STW
      {
        ?stw skos:prefLabel ?label
      }
      UNION
      {
        ?stw skos:altLabel ?label
      }
      # include labels from matching concepts in other thesauri
      UNION
      {
        ?stw skos:exactMatch/skos:prefLabel ?label
      }
      UNION
      {
        ?stw skos:exactMatch/skos:altLabel ?label
      }
    }
    bind(strafter(str(?foreign), 'gnd/') as ?id)
  }
}
group by ?id
