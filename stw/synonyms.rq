# List of synonyms (for use in RePEc/Ideas and maybe other applications)
#
# Uses STW descriptors as well as exact matching concepts from other thesauri
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext:  <http://zbw.eu/namespaces/zbw-extensions/>
#
SELECT ?concept (str(sample(?prefLabel)) AS ?conceptLabel) (group_concat(?synonym;
    separator = ' | ') AS ?synonyms)
WHERE {
  SELECT DISTINCT ?concept ?prefLabel (lcase(str(?label)) AS ?synonym)
  WHERE {
    VALUES ( ?language ) {
      ( "en" )
    }
    ?concept a zbwext:Descriptor ;
             skos:prefLabel ?prefLabel .
    FILTER (lang(?prefLabel) = ?language)
    {
      # include all labels from STW
      {
        ?concept skos:prefLabel ?label
      }
      UNION
      {
        ?concept skos:altLabel ?label
      }
      # include labels from matching concepts in other thesauri
      UNION
      {
        ?concept skos:exactMatch/skos:prefLabel ?label
      }
      UNION
      {
        ?concept skos:exactMatch/skos:altLabel ?label
      }
    }
    # activate next line for synonyms in a single language
    ##FILTER (lang(?label) = ?language)
  }
}
GROUP BY ?concept
ORDER BY ?conceptLabel

