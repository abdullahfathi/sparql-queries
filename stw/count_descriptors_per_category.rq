# Number of descriptors per category
#
prefix dcterms: <http://purl.org/dc/terms/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?thsys (str(?thsysLabelLang) as ?thsysLabel) (str(count(distinct ?descriptor)) as ?descriptorCount)
where {
  ?thsys a zbwext:Thsys ;
         skos:prefLabel ?thsysLabelLang .
  filter(lang(?thsysLabelLang) = 'de')
  # 
  # filter out all categories with sub-categories
  filter(not exists {
      ?thsys skos:narrower ?x .
      ?x a zbwext:Thsys .
    }
  )
  ?thsys skos:narrower ?descriptor .
  ?descriptor a zbwext:Descriptor .
}
group by ?thsys ?thsysLabelLang
order by count(distinct ?descriptor)

