prefix  skos:   <http://www.w3.org/2004/02/skos/core#>
prefix  zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Counts the descritors attached to thsys branches on the first
# and second level.
#
# Aggregates descriptors recursivly within the thsys hierarchy,
# but regards only those directly narrower to a thsys category
# (disregarding inter-descriptor narrower relationships).
#
select ?category (str(?label) as ?categoryLabel) (str(?total) as ?totalDescriptors)
where
{
  {
    select (?top as ?category) (count(?s) as ?total) (max(?topLabel) as ?label)
    where {
      <http://zbw.eu/stw> skos:hasTopConcept ?top .
      ?top skos:prefLabel ?topLabel .
      ?top skos:narrower* ?thsys .
      ?thsys a zbwext:Thsys .
      ?thsys skos:narrower ?s.
      ?s a zbwext:Descriptor .
      filter ( (lang(?topLabel) = "en") )
    }
    group by ?top
  } union {
    select (?second as ?category) (count(?s) as ?total) (max(?secondLevelLabel) as ?label)
    where {
      <http://zbw.eu/stw> skos:hasTopConcept ?top .
      ?top skos:narrower ?second .
      ?second a zbwext:Thsys .
      ?second skos:prefLabel ?secondLevelLabel .
      ?second skos:narrower* ?thsys .
      ?thsys a zbwext:Thsys .
      ?thsys skos:narrower ?s .
      ?s a zbwext:Descriptor .
      filter ( (lang(?secondLevelLabel) = "en") )
    }
    group by ?second
  }
}
order by ?categoryLabel

