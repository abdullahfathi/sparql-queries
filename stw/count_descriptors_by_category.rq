# Counts the descritors attached to thsys branches on the first
# and second level.
#
# Aggregates descriptors recursivly within the thsys hierarchy,
# but regards only those directly narrower to a thsys category
# (disregarding inter-descriptor narrower relationships).
#
prefix  skos:   <http://www.w3.org/2004/02/skos/core#>
prefix  zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?category (str(?label) as ?categoryLabel) (str(?total) as ?totalDescriptors)
where
{
  values ( ?language ) {
    ( "en" )
  }
  {
    select (?top as ?category) (count(distinct ?s) as ?total)
    where {
      <http://zbw.eu/stw> skos:hasTopConcept ?top .
      ?top skos:narrower* ?thsys .
      ?thsys a zbwext:Thsys .
      ?thsys skos:narrower ?s .
      ?s a zbwext:Descriptor .
    }
    group by ?top
  } union {
    select (?second as ?category) (count(distinct ?s) as ?total)
    where {
      <http://zbw.eu/stw> skos:hasTopConcept ?top .
      ?top skos:narrower ?second .
      ?second a zbwext:Thsys .
      ?second skos:narrower* ?thsys .
      ?thsys a zbwext:Thsys .
      ?thsys skos:narrower ?s .
      ?s a zbwext:Descriptor .
    }
    group by ?second
  }
  ?category skos:prefLabel ?label .
  filter(lang(?label) = ?language)
}
order by ?categoryLabel
