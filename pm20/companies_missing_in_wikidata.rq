# Extract information for creating missing company items in Wikidata
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX frapo: <http://purl.org/cerif/frapo/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?pm20 ?pm20Label ?mnm (?mnmId as ?mnmLabel) ?wdLabel ?incepted ?abandoned ?gndId
?pm20Id ?classQid (?orgType as ?descrDe) ?descrEn
where {
  #
  # determine for which entries we want to create items
  {
#    # already checked entries of M-n-m catalog
#    graph ?mnmGraph {
#      # catalog and state
#      # (late binding for ?mnmGraph!)
#      values ( ?catalogId ?maxCheckedId ) {
#        ( '623' '25376941' )
#      }
#      bind(uri(concat('http://zbw.eu/beta/mix-n-match-', ?catalogId, '/ng')) as ?mnmGraph)
#      #
#      ?pm20 dc:identifier ?mnmId .
#      filter(?mnmId < ?maxCheckedId)
#      bind(uri(concat('https://tools.wmflabs.org/mix-n-match/#/entry/', ?mnmId)) as ?mnm)
#      bind(strafter(str(?pm20), 'http://purl.org/pressemappe20/folder/') as ?pm20Id)
#    }
    ?pm20 a zbwext:CompanyFolder ;
        skos:prefLabel ?pm20Label ;
        dct:identifier ?pm20Id ;
        zbwext:adjustedLabel ?name ;
        frapo:hasCountryCode ?country ;
        zbwext:totalDocCount ?docCount .
    # this is the filter for defining the separate mnm catalogs (parts)
    #filter(?country in ('GB', 'US', 'CA'))
    filter(?country in ('NL', 'ID', 'AO'))
  } minus {
    # minus all entries which are linked to WD items
    service <https://query.wikidata.org/sparql> {
      ?wd wdt:P4293 ?pm20Id .
    }
  }
  # organization type mappings
  values ( ?orgType ?classQid ?descrEn ) {
    ( 'Unternehmen'@de 'Q4830453' 'business' )
    ( 'Kolonialgesellschaft'@de 'Q1700154' 'colonial society' )
    ( 'Verein'@de 'Q48204' 'association' )
  }
  #
  # apply further restrictions:
  # restrict to certain types of organization
  ?pm20 dc:type ?orgType .
  # remove entries with suspect labels including '()'
  ##filter(!contains(?pm20Label, '('))
  #
  # now get further information to add to WD
  #
  optional {
    ?pm20 schema:foundingDate ?incepted .
  }
  optional {
    ?pm20 schema:dissolutionDate ?abandoned .
  }
  optional {
    ?pm20 gndo:gndIdentifier ?gndId .
  }
}
order by desc(?docCount) ?name

