# Get the industry and nace assignment from pm20
# (only folders with documents)
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX esco: <http://data.europa.eu/esco/model#>
PREFIX frapo: <http://purl.org/cerif/frapo/>
PREFIX hint: <http://www.bigdata.com/queryHints#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select distinct ?pm20Id ?pm20 ?pm20Label ?skCode ?skIndustry ?skIndustryLabel ?naceCode ?nace ?naceLabel
where {
  ?pm20 a zbwext:CompanyFolder ;
        skos:prefLabel ?pm20Label ;
        dct:identifier ?pm20Id ;
        zbwext:totalDocCount [] .
  optional {
    ?pm20 schema:industry ?skIndustryLabelLang
    filter(lang(?skIndustryLabelLang) = 'de')
    bind(str(?skIndustryLabelLang) as ?skIndustryLabel)
    graph <http://zbw.eu/beta/sk/ng> {
      ?skIndustry skos:prefLabel ?skIndustryLabelLang ;
                  skos:notation ?skCode .
    }
  }
  optional {
    ?pm20 esco:hasNACECode ?naceCode .
    bind(concat('.', ?naceCode) as ?ifisNaceCode)
    graph <http://zbw.eu/beta/na/ng> {
      ?nace skos:notation ?ifisNaceCode .
      ?nace skos:prefLabel ?naceLabelLang
      filter(lang(?naceLabelLang) = 'de')
      bind(str(?naceLabelLang) as ?naceLabel)
    }
  }
  #filter(bound(?nace))
}
order by ?pm20Id

