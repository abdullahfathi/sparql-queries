# Subject and ware folder count by country
#
PREFIX schema: <http://schema.org/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
#
select ?country ?countryLabel ?shCount (str(sum(?shCnt)) as ?shCountLabel)
?waCount (str(sum(?waCnt)) as ?waCountLabel)
(str(sum(?docCount)) as ?docs)
where {
  ?pm20 zbwext:country ?country ;
        zbwext:freeDocCount ?docCount .
  bind(if(contains(str(?pm20), '/sh/'), 1, 0) as ?shCnt)
  bind(if(contains(str(?pm20), '/wa/'), 1, 0) as ?waCnt)
  #
  graph ?g {
    ?country dct:identifier ?countryId ;
             skos:prefLabel ?countryLabelLang .
    filter(lang(?countryLabelLang) = 'de')
    bind(str(?countryLabelLang) as ?countryLabel)
  }
  bind(uri(concat('http://webopac.hwwa.de/PresseMappe20/Digiview_MENU.cfm?T=S&G=', ?countryId)) as ?shCount)
  bind(uri(concat('http://webopac.hwwa.de/PresseMappe20/Digiview_MENU.cfm?T=W&G=', ?countryId)) as ?waCount)
}
group by ?country ?countryLabel ?shCount ?waCount
order by desc(sum(?shCnt))

