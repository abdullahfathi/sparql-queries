# List company folders active in colonial affairs
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX schema: <http://schema.org/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
#
select ?pm20 ?pm20Label ?fromTo ?industries (group_concat(?broaderLocation; separator='; ') as ?broaderLocations)
where {
select ?pm20 ?pm20Label ?fromTo (group_concat(?industry; separator = '; ') as ?industries) ?broaderLocation
where {
  ?pm20 a zbwext:CompanyFolder ;
    dc:type 'Kolonialgesellschaft' ;
    skos:prefLabel ?pm20Label .
  #
  optional {
    ?pm20 skos:notation ?nta .
    bind(strbefore(?nta, ' ') as ?geoNta)
  }
  # Restrict to Germany and German colonies
  filter( bound(?geoNta) && ?geoNta in ('A9','A10','A11','B103','C58','C65','C87','C110','D10','D60' ))
  #
  optional {
    ?pm20 schema:industry ?industry .
  }
  optional {
    ?pm20 zbwext:fromTo ?fromTo .
  }
  optional {
    ?pm20 zbwext:broaderLocation ?broaderLocation .
  }
}
group by ?pm20 ?pm20Label ?fromTo ?broaderLocation
}
group by ?pm20 ?pm20Label ?fromTo ?industries
order by ?pm20Label

