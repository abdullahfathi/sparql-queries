# Create input for Wikidata Mix'n'match for PM20 companies
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX frapo: <http://purl.org/cerif/frapo/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?id ?name ?description
where {
  {
    select ?id ?name ?docCount ?prefLabel ?fromTo
    (group_concat(distinct ?location; separator = '; ') as ?locations)
    (group_concat(distinct ?industry; separator='; ') as ?industries)
    (group_concat(distinct ?note; separator='; ') as ?notes)
    where {
      values ( ?language ?filterLang ) {
        ( 'de' 'de' )
      }
      # query restricts result to folders with documents
      ?pm20 a zbwext:CompanyFolder ;
        dct:identifier ?id ;
        skos:prefLabel ?prefLabel ;
        zbwext:adjustedLabel ?name ;
        frapo:hasCountryCode ?country ;
        zbwext:totalDocCount ?docCount .
      #
      # this is the filter for defining the separate mnm catalogs (parts)
      bind(
        if(?country in ('NL', 'ID', 'AO'), 'nl',
          if(?country in ('GB', 'US', 'CA', 'AU', 'ZA', 'IE', 'MY', 'ZW'), 'en',
            if(?country in ('FR', 'VN', 'BE', 'CD', 'LU', 'DZ'), 'fr',
              if(?country in ('DE', 'AT', 'CH', 'HU', 'TZ', 'PL', 'CZ', 'WS'), 'de',
                'und')))) as ?wikiLang)
      filter(?wikiLang = ?filterLang)
      #
      optional {
        ?pm20 zbwext:fromTo ?fromTo .
      }
      optional {
        ?pm20 schema:location ?location .
        filter(lang(?location) = ?language)
      }
      optional {
        ?pm20 schema:industry ?industry .
        filter(lang(?industry) = ?language)
      }
      optional {
        ?pm20 skos:note ?note .
      }
    }
    group by ?id ?name ?docCount ?prefLabel ?fromTo
  }
  bind(concat('{', str(?docCount), '} ', if(bound(?fromTo), concat(?fromTo, ', '), ''), if(bound(?locations), ?locations, ''),
      ' (', if(bound(?industries), ?industries, ''), '): ', $prefLabel, ' -. ', if(bound(?notes), ?notes, '')) as ?description)
}
order by desc(?docCount) ?name

