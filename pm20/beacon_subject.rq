# create beacon file mappings for subjects
#
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
select (concat(?gndId, ' | ', ?count, ' | ', ?categoryId) as ?beaconEntry)
where {
  {
    select ?gndId (str(count(?pm20)) as ?count) (sample(?conceptId) as ?categoryId)
    where {
      graph <http://zbw.eu/beta/subject/ng> {
        ?concept gndo:gndIdentifier ?gndId .
      }
      ?pm20 a zbwext:SubjectFolder ;
            zbwext:subject ?concept .
      bind(strafter(str(?concept), '/subject/i/') as ?conceptId)
    }
    group by ?gndId
  }
}

