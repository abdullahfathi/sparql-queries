# Extract information for creating missing properties in Wikidata
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
#
select ?qid ?pm20Id ?wdProperty ?value
where {
  values ( ?srcType ?srcProperty ?wdProperty ) {
    ( zbwext:PersonFolder gndo:gndIdentifier wdt:P227 )
  }
  #
  # all pm20 entries with a certain property
  {
    ?pm20 a ?srcType ;
          ?srcProperty ?value .
    bind(strafter(str(?pm20), 'http://purl.org/pressemappe20/folder/') as ?pm20Id)
  } minus {
    # minus all WD items for which the property alerady exists
    service <https://query.wikidata.org/sparql> {
      ?wd wdt:P4293 ?pm20Id ;
          ?wdProperty [] .
    }
  }
  ?pm20 zbwext:wdIdentifier ?qid .
}
