# Distinct values constraint violations (multiple exact matches from an item)
#
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#
select distinct ?wd ?wdLabel ?relation ?relationLabel ?geoCode
where {
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    select ?wd ?wdLabel ?relation ?relationLabel ?geoCode
    where {
      # identify items with multiple PM20 geo code values
      {
        select distinct  ?geoCode (count(?wd) as ?wdCount)
        where {
          # statements on property PM20 geo code
          ?wd p:P8483 ?statement .
          # mapping relation exact match
          ?statement pq:P4390 wd:Q39893449 ;
                     ps:P8483 ?geoCode .
        }
        group by ?geoCode 
        having (?wdCount > 1)
      }
      # expand to all existing PM20 geo codes and relations for the item
      ?wd wdt:P8483 ?geoCode .
      filter(isLiteral(?geoCode))
      ?wd p:P8483/pq:P4390 ?relation .
      #
      # get label for the wd item, if exists
      optional {
        ?wd rdfs:label ?label .
        filter(lang(?label) = 'en')
        bind(str(?label) as ?wdLabel)
      }
      ?relation rdfs:label ?relationLabelLang .
      filter(lang(?relationLabelLang) = 'en')
      bind(str(?relationLabelLang) as ?relationLabel)
    }
  }
}
order by ?geoCode desc(?relation) ?wdLabel

