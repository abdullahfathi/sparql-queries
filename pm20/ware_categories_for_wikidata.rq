# Extract information for creating ware category items in Wikidata
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?notationLong ?labelDe ?labelEn
('Warensystematikstelle des Pressearchiv 20. Jahrhundert' as ?descrDe)
('commodities/wares category of the 20th Century Press Archives' as ?descrEn)
?wareId
('Q111973176' as ?classQid)
('Q113375556' as ?partOf)
where {
  #
  graph <http://zbw.eu/beta/ware/ng> {
    ?cat a skos:Concept ;
      dct:identifier ?wareId .
    #
    ?cat skos:prefLabel ?labelLangDe .
    filter(lang(?labelLangDe) = 'de')
    bind(str(?labelLangDe) as ?labelDe)
    optional {
      ?cat skos:prefLabel ?labelLangEn .
      filter(lang(?labelLangEn) = 'en')
      bind(str(?labelLangEn) as ?labelEn)
    }
    optional {
      ?cat zbwext:folderCount ?folderCount .
    }

    # temporary restriction
    filter(bound(?folderCount))
  #
  # filter out categories already existing in WD
  } minus {
    service <https://query.wikidata.org/sparql> {
      ?wd wdt:P10890 ?wareId .
    }
  }
}
order by ?labelDe
limit 100

