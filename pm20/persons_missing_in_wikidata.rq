# Extract information for creating missing company items in Wikidata
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
#
select ?pm20 ?pm20Label ?pm20Id ?classQid ?gndId
where {
  #
  # determine for which entries we want to create items
  {
    ?pm20 a zbwext:PersonFolder .
    bind(strafter(str(?pm20), 'http://purl.org/pressemappe20/folder/') as ?pm20Id)
  } minus {
    # minus all entries which are linked to WD items
    service <https://query.wikidata.org/sparql> {
      ?wd wdt:P4293 ?pm20Id .
    }
  }
  #
  ?pm20 skos:prefLabel ?pm20Label ;
        zbwext:freeDocCount ?docCount .
  #
  # apply further restrictions:
  # only if folder has documents
  filter(?docCount > 0)
  #
  bind('Q5' as ?classQid)
  #
  optional {
    ?pm20 gndo:gndIdentifier ?gndId .
  }
}

