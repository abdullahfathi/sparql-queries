# List of all companies/organizations
#
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX schema: <http://schema.org/>
#
SELECT ?type ?pm20 ?pm20Label ?fromTo ?notation (group_concat(distinct ?industry;
    separator='; ') as ?industries) (str(?docCount) as ?docs) (str(?repCount) as ?reports)
WHERE {
  # TODO currently only dummy
  values ( ?language ) {
    ( 'de' )
  }
  ?pm20 a zbwext:CompanyFolder ;
        skos:prefLabel ?pm20Label ;
        schema:industry ?industry ;
        skos:notation ?notation ;
        dc:type ?type ;
        zbwext:freeDocCount ?docCount .
  # only companies with documents
  filter(?docCount > 0)
  #
  # restrict by country notation (see
  # http://zbw.eu/beta/skosmos/pm20ag/en/page/157538)
  ##filter(strstarts(?notation, 'A9') || strstarts(?notation, 'A10')) # Germany
  ##filter(strstarts(?notation, 'A22')) # UK
  ##filter(strstarts(?notation, 'A28')) # France
  #
  optional {
    ?pm20 zbwext:fromTo ?fromTo .
  }
  optional {
    ?pm20 zbwext:reportCount ?repCount .
  }
  # only companies with business reports
  ##filter(?repCount > 0)
}
group by ?pm20 ?pm20Label ?fromTo ?type ?docCount ?repCount ?notation
order by ?pm20Label

