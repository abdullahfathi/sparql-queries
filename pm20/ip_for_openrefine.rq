# Create a table of ip entries for OpenRefine
# (ip = Alte HWWA Warenklassifikation)
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX schema: <http://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
#
select distinct  (str(?s) as ?descriptor) (str(count(distinct ?pm20)) as ?cnt)
(sample(?byGndX) as ?byGnd) ?byLabelsDe ?byLabelsEn
('' as ?rel) ?broaderDe ?notation (str(?note) as ?noteDe) ?id (max(?gndIdentifier) as ?gndId) ?s
where {
  graph <http://zbw.eu/beta/ware/ng> {
    ?s skos:prefLabel ?labelLangDe ;
       skos:notation ?notation ;
       dct:identifier ?id .
    filter(lang(?labelLangDe) = 'de')
    bind(str(?labelLangDe) as ?byLabelsDe)
    optional {
      ?s skos:prefLabel ?labelLangEn .
      filter(lang(?labelLangEn) = 'en')
      bind(str(?labelLangEn) as ?byLabelsEn)
    }
    #
    # gnd for indirect mapping
    optional {
      ?s gndo:gndIdentifier ?gndIdentifier .
    }
    bind(if(bound(?gndIdentifier), 'some gnd', '') as ?byGndX)
    #
#    filter(not exists {
#        ?s skos:exactMatch ?wd .
#        filter(contains(str(?wd), 'wikidata'))
#      }
#    )
    optional {
      ?broader skos:narrower ?s ;
               skos:prefLabel ?broaderLangDe .
      filter(lang(?broaderLangDe) = 'de')
      bind(str(?broaderLangDe) as ?broaderDe)
    }
    optional {
      ?s skos:scopeNote ?note .
      filter(!contains(?note, 'folderCount'))
    }
  }
  optional {
    ?pm20 zbwext:ware ?s .
  }
}
group by ?s ?broaderDe ?id ?notation ?byLabelsEn ?byLabelsDe ?note
having ( ?cnt != "0" )
order by ?notation ?id

