# Create a table ip entries for OpenRefine
# (ip = Alte HWWA Warenklassifikation)
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX schema: <http://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
#
select distinct ?broaderDe ?id ?notation ?labelEn ?labelDe (str(?note) as ?noteDe) ?gndId
where {
  graph <http://zbw.eu/beta/ip/ng> {
    ?s skos:prefLabel ?labelLangDe ;
       skos:notation ?notation ;
       dct:identifier ?id .
    filter(lang(?labelLangDe) = 'de')
    bind(str(?labelLangDe) as ?labelDe)
    optional {
      ?s skos:prefLabel ?labelLangEn .
      filter(lang(?labelLangEn) = 'en')
      bind(str(?labelLangEn) as ?labelEn)
    }
    #
    # gnd for indirect mapping
    optional {
      ?s gndo:gndIdentifier ?gndId .
    }
    #
#    filter(not exists {
#        ?s skos:exactMatch ?wd .
#        filter(contains(str(?wd), 'wikidata'))
#      }
#    )
    optional {
      ?broader skos:narrower ?s ;
               skos:prefLabel ?broaderLangDe .
      filter(lang(?broaderLangDe) = 'de')
      bind(str(?broaderLangDe) as ?broaderDe)
    }
    optional {
      ?s skos:note ?note .
    }
    filter(bound(?note) && contains(?note, 'folderCount'))
  }
}
order by ?notation ?id

