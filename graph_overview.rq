# Count triples and distinct subjects per graph
#
select (str(?tCount) as ?tripleCount) (str(?sCount) as ?distinctSubjectCount) (str(?cCount) as ?distinctClassCount) (str(?pCount) as ?distinctPropertyCount) ?graphName
where {
  {
    # the default graph
    select (count(*) as ?tCount) (count(distinct ?s) as ?sCount) (count(distinct ?class) as ?cCount) (count(distinct ?p) as ?pCount)
    where {
      ?s ?p ?o .
      ?s a ?class .
    }
  } union {
    # the named graphs
    select (count(*) as ?tCount) (count(distinct ?s) as ?sCount) (count(distinct ?class) as ?cCount) (count(distinct ?p) as ?pCount) ?graphName
    where {
      graph ?graphName {
        ?s ?p ?o .
        optional {
          ?s a ?class .
        }
      }
    }
    group by ?graphName
    order by ?graphName
  }
}

