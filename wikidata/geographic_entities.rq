# Selection of geographich entities
#
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX v: <http://www.wikidata.org/prop/statement/>
PREFIX bd: <http://www.bigdata.com/rdf#>
#
SELECT ?item ?itemLabel
WHERE {
  {
    SELECT ?item ?itemLabel WHERE {
      # instances of ...
      {
        # state of Germany
        ?item p:P31/v:P31 wd:Q1221156
      } union {
        # country
        ?item p:P31/v:P31 wd:Q6256
      } union {
        # geographic region
        ?item p:P31/v:P31 wd:Q82794
      } union {
        # political union
        ?item p:P31/v:P31 wd:Q1140229
      }
      # 
      SERVICE wikibase:label {
        # returns German (preferred) or English label, or 
        # entity name if neither exists
        bd:serviceParam wikibase:language "de,en" .
      }
    }
  }
  # autosuggest string to inject
  filter(regex(str(?itemLabel), "^ost", "i"))
  # filter out entity names
  filter(!strends(str(?item), ?itemLabel))
}
order by str(?itemLabel)

