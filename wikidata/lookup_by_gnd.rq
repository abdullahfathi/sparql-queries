# Lookup wikidata URI by GND id
#
# Must use SPARQL GET request (wikidata does not support POST - 
# see https://phabricator.wikimedia.org/T112151)
#
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX v: <http://www.wikidata.org/prop/statement/>
PREFIX q: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
select *
where {
  # GND ID of John Maynard Keynes and of Currency union
  values ( ?gndId ) {
    ( '118561804' ) 
    ( '4188829-7' )
  }
  # look up information
  ?s wdt:P227 ?gndId .
  ?s rdfs:label ?label .
  filter (lang(?label) in ('en', 'de'))
  #
  # build page link from language tag, replacing blanks with underscores
  # CAUTION: rdfs:label give the label of the wikidata point, not the page title!!!
  bind(uri(concat('http://', lang(?label), '.wikipedia.org/wiki/', replace(str(?label), ' ', '_'))) as ?pagelink)
}
