# Statistics about economics-related gnd persons (from ebds)
# and how many of them are in wikidata
#
# Must run against private ebds endpoint!
#
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?pubThreshold
(sum(?persInEb) as ?ebPersCount)  # all gnd in the ebds dataset
(sum(?persInWd) as ?ebPersWdCount)  # of these, in Wikidata
(concat(str(round((?ebPersWdCount/?ebPersCount)*1000)/10), ' %') as ?ebPersWdPct)
where {
  values (?pubThreshold) {
    ( 0 ) ( 1 ) ( 2 ) ( 10 ) ( 30 ) ( 50 ) ( 100 ) ( 150 )
  }
  {
    select ?gndId (max(?ebCount) as ?ebPubCount) (max(?inWd) as ?persInWd) (max(?inEb) as ?persInEb)
    where {
      {
        graph <http://zbw.eu/beta/ebCountActor/ng> {
          ?ebPers a gndo:DifferentiatedPerson ;
            zbwext:econbizCount ?ebCount .
        }
        bind(strafter(str(?ebPers), '/gnd/') as ?gndId)
      } union {
        service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
          ?wdPers wdt:P31 wd:Q5 ;
            wdt:P227 ?gndId .
        }
        bind(0 as ?ebCount)
      }
      bind(if(bound(?ebPers), 1, 0) as ?inEb)
      bind(if(bound(?wdPers), 1, 0) as ?inWd)
    }
    group by ?gndId
  }
  # a threshold > 0 excludes all gndIds not in EBDS
  filter(?ebPubCount > ?pubThreshold)
}
group by ?pubThreshold
order by asc(?pubThreshold)
