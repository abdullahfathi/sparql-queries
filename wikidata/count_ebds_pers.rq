# Statistics about economics-related gnd persons (from ebds)
# and how many of them are in wikidata
#
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select ?publThreshold
(count(?econPers) as ?econPersCount)  # all gnd in the econ_pers dataset
#(sum(?ebPubCount) as ?econPersPubCount)
(count(?econPersWd) as ?econPersWdCount)  # of these, in Wikidata
#(sum(?wdPubCount) as ?econPersWdPubCount) 
(concat(str(round((?econPersWdCount/?econPersCount)*1000)/10), ' %') as ?econPersWdPct)
(sum( ?isWdEconomist) as ?wdOccupationEconomistCount)   # econPersWd with occupation "economist"
(concat(str(round((?wdOccupationEconomistCount/?econPersWdCount)*1000)/10), ' %') as ?wdOccupationEconomistPct)
where {
  values (?publThreshold) {
    ( 0 ) ( 1 ) ( 2 ) ( 10 ) ( 30 ) ( 50 ) ( 150 )
  }
  graph <http://zbw.eu/beta/wikidata/ebCount/ng> {
    ?econPers zbwext:econbizCount ?ebPubCount ;
              dcterms:identifier ?gndId .
  }
  # increase publThreshold to restrict to more relevant persons
  filter(?ebPubCount > ?publThreshold)
  #
  # which of these persons are in wikidata?
  optional {
    ?econPersWd wdt:P227 ?gndId .
  }
  # count publications of wd persons separately
#  bind(if(bound(?econPersWd), ?ebPubCount, 0) as ?wdPubCount)
  #
  # is the wd occupation of this person "economist"?
  bind(if(bound(?econPersWd) && exists {
        ?econPersWd wdt:P106 wd:Q188094
      }, 1, 0) as ?isWdEconomist)
}
group by ?publThreshold
order by asc(?publThreshold)
