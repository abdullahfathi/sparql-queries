# List the most frequent persons in EconBiz (by GND) and links
# to Wikidata or to a search for all their names in Wikidata
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ebds: <http://zbw.eu/beta/ebds/>
PREFIX ebsrc: <http://zbw.eu/beta/ebds/source/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select
(uri(concat('http://www.econbiz.de/Search/Results?lookfor=pnd%3A', ?gndId)) as ?ebPublications)
(str(?count) as ?ebPublicationsLabel)
?gndId ?gndPers ?gndPersLabel ?life
?wdLink ?wdLinkLabel
where {
  service <http://172.16.10.102:3030/ebds/query> {
    {
      select (count(distinct ?pub) as ?count) ?gndPers
      where {
        graph <http://zbw.eu/beta/ebds/ng> {
          values ( ?actor ) {
            ( dcterms:creator ) ( dcterms:contributor ) ( dcterms:subject )
          }
          ?pub ?actor ?gndPers .
        }
      }
      group by ?gndPers
      having (?count > 150)
    }
    graph <http://zbw.eu/beta/ebds/gnd/ng> {
      ?gndPers a gndo:DifferentiatedPerson ;
        gndo:preferredNameForThePerson ?gndPersLabel .
      optional {
        ?gndPers gndo:dateOfBirth ?birthDate .
      }
      optional {
        ?gndPers gndo:dateOfDeath ?deathDate .
      }
      bind(if(bound(?birthDate), str(?birthDate), '') as ?birth)
      bind(if(bound(?deathDate), str(?deathDate), '')  as ?death)
      bind(concat(?birth, ' - ', ?death) as ?life)
    }
  }
  bind(strafter(str(?gndPers), '/gnd/') as ?gndId)
  bind(uri(concat('http://zbw.eu/beta/sparql-lab/?endpoint=http://172.16.10.102:3030/wikidata/query&queryRef=https://api.github.com/repos/zbw/sparql-queries/contents/wikidata/search_person_by_gnd_names.rq&gndId=', $gndId)) as ?wdSearchLink)
  optional {
    ?wd wdt:P227 ?gndId ;
      rdfs:label ?wdLabelLang .
    filter(lang(?wdLabelLang) = 'en')
    bind(str(?wdLabelLang) as ?wdLabel)
  }
  bind(if(bound(?wd), ?wd, ?wdSearchLink) as ?wdLink)
  bind(if(bound(?wd), ?wdLabel, 'SEARCH in Wikidata') as ?wdLinkLabel)

}
order by desc(?count)

