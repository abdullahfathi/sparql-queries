# repec authors by publication count
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ebds: <http://zbw.eu/beta/ebds/>
PREFIX ebsrc: <http://zbw.eu/beta/ebds/source/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?count ?pers ?persLabel ?gndId
(uri(concat('http://www.econbiz.de/Search/Results?lookfor=pnd%3A', ?gndId)) as ?ebLink)
('EconBiz publications' as ?ebLinkLabel)
?wdSearchLink
('Search in Wikidata' as ?wdSearchLinkLabel)
where {
  service <http://172.16.10.102:3030/ebds/query> {
    {
      select (count(distinct ?pub) as ?count) ?pers 
      where {
        graph <http://zbw.eu/beta/ebds/ng> {
          values ( ?actor ) {
            ( dcterms:creator ) ( dcterms:contributor ) ( dcterms:subject )
          }
          ?pub ?actor ?pers .
        }
      }
      group by ?pers
      having (?count > 150)
    }
    graph <http://zbw.eu/beta/ebds/gnd/ng> {
      ?pers a gndo:DifferentiatedPerson ;
        gndo:preferredNameForThePerson ?persLabel .
    }
  }
  bind(strafter(str(?pers), '/gnd/') as ?gndId)
  bind(concat('http://zbw.eu/beta/sparql-lab/?endpoint=http://zbw.eu/beta/sparql/gnd/query&queryRef=https://api.github.com/repos/jneubert/sparql-queries/contents/gnd/search_person_by_gnd_names.rq&name=', $gndId) as ?wdSearchLink)
}
order by desc(?count)

