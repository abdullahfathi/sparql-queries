# Lookup wikidata URI, label and en+de wikipedia site links by RePEc id
#
# Must use SPARQL GET request (wikidata does not support POST - 
# see https://phabricator.wikimedia.org/T112151)
#
PREFIX wd: <http://www.wikidata.org/entity/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX v: <http://www.wikidata.org/prop/statement/>
PREFIX q: <http://www.wikidata.org/prop/qualifier/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
select ?repecId ?wd ?wdLabel ?sitelink
where {
  # RePEc ID of Esther Duflo and Peter Nijkamp
  values ( ?repecId ) {
    ( 'pdu166' )
    ( 'pni111' ) 
  }
  # look up wikidata items and labels
  ?wd wdt:P227 ?repecId ;
      rdfs:label ?wdLabel .
  #
  # get site links (only from de/en wikipedia sites)
  ?sitelink schema:about ?wd ;
            schema:inLanguage ?language .
  filter (contains(str(?sitelink), 'wikipedia'))
  filter (lang(?wdLabel) = ?language && ?language in ('en', 'de'))
}

