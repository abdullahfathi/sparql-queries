# Internationally prominent economists
# (with entries in three or more Wikipedias)
#
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#
select ?wdLabel ?wd ?desc (str(count(?sitelink)) as ?siteCount) ?gndId
where {
  values (?occupation) {
    (<http://www.wikidata.org/entity/Q188094>) # economist
  }
  ?wd wdt:P106 ?occupation .
  #
  # get site links
  ?sitelink schema:about ?wd .
  # filter out wikicite/wikicommons etc. links
  filter(contains(str(?sitelink), 'wikipedia.org/'))
  #
  # not all items have labels in English
  optional {
    ?wd rdfs:label ?wdLabelLang
    filter(lang(?wdLabelLang) = 'en')
    bind(str(?wdLabelLang) as ?wdLabel)
  }
  optional {
    ?wd schema:description ?descEn
    filter(lang(?descEn) = "en")
    bind(str(?descEn) as ?desc)
  }
  optional {
    ?wd wdt:P227 ?gndId .
  }
}
group by ?wd ?wdLabel ?gndId ?desc
having (count(?sitellink) >= 3)
order by desc(xsd:integer(?siteCount))

