# Internationally prominent economists
# (with entries in two or more Wikipedias)
#
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#
select ?wdLabel ?wd (count(?sitelink) as ?siteCount)
where {
  values (?occupation) {
    (<http://www.wikidata.org/entity/Q188094>) # economist
  }
  ?wd wdt:P106 ?occupation .
  #
  # get site links
  ?sitelink schema:about ?wd .
  # filter out wikicite/wikicommons etc. links
  filter(contains(str(?sitelink), 'wikipedia.org/'))
  #
  # not all items have labels in English
  optional {
    ?wd rdfs:label ?wdLabelLang
    filter(lang(?wdLabelLang) = 'en')
    bind(str(?wdLabelLang) as ?wdLabel)
  }
}
group by ?wd ?wdLabel
having (?siteCount > 1)
order by desc(?siteCount)

