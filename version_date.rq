# Try to identify the version and/or date of a dataset
#
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
#
select distinct ?s ?datasetClass ?property ?value
where {
  # try different strategies
  {
    # look for version
    # suppose that owl:versionInfo is only used once
    select ?s ?datasetClass ?property ?value
    where {
      bind(owl:versionInfo as ?property)
      ?s ?property ?value .
    }
  } union {
    # look for dates
    # restrict to classes which typically occur once,
    # because the date properties may be in use for multiple subjects
    select ?s ?datasetClass ?property ?value
    where {
      {
        ?s a void:Dataset
      } union {
        ?s a skos:ConceptScheme
      }
      ?s ?property ?value .
    }
    values ( ?property ) {
      ( dc:date)
      ( dcterms:date )
      ( dcterms:created )
      ( dcterms:issued )
      ( dcterms:modified )
    }
  }
}

