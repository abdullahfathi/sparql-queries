# all individual authors named "Fischer, Peter"
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX gndo: <http://d-nb.info/standards/elementset/gnd#>
#
select (str(count(distinct ?pub)) as ?pubCount) ?actor ?life ?info
where {
  service <http://zbw.eu/beta/sparql/gnd/query> {
    ?actor a gndo:DifferentiatedPerson;
           gndo:preferredNameForThePerson "Fischer, Peter" .
    optional {
      ?actor gndo:dateOfBirth ?birth .
      bind(str(year(?birth)) as ?life)
    }
    optional {
      ?actor gndo:biographicalOrHistoricalInformation ?info .
    }
  }
  ?pub dct:creator ?actor .
}
group by ?actor ?life ?info
order by desc(count(distinct ?pub))
