# Get GND ids for Wikidata pages without GND id, but having VIAF id,
# making use of VIAF sameAs data
#
# against viaf endpoint
#
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX schema: <http://schema.org/>
#
select distinct ?wdId ?gndId
#select *
where {
  # all wikidata items with viaf id, which have no gnd id
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    ?wd wdt:P31 wd:Q5 ;
        wdt:P214 ?viafId .
    filter(not exists {
        ?wd wdt:P227 [] .
      })
  }
  bind(strafter(str(?wd), "http://www.wikidata.org/entity/") as ?wdId)
  bind(uri(concat("http://viaf.org/viaf/", $viafId)) as ?viaf)
  #
  # of these, all which have gnd ids in viaf
  ?viaf schema:sameAs ?gnd
  filter(contains(str(?gnd), "http://d-nb.info/gnd/"))
  bind(strafter(str(?gnd), "http://d-nb.info/gnd/") as ?gndId)
  #
  # optionally, restrict to economists
}
